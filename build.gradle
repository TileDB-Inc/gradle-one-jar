import org.gradle.api.artifacts.maven.MavenDeployment

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: "com.gradle.plugin-publish"

group = 'io.tiledb'
version = '1.0.6'
ext.packaging = 'jar'

def pluginName = 'gradle-one-jar'
def pluginDesc = 'The gradle-one-jar project is a Gradle plugin that uses One-JAR, a specialized Class-Loader written by Simon Tuffs (http://one-jar.sourceforge.net/), for building self-contained executable jars that include all dependencies.'
def pluginHome = 'https://github.com/TileDB/gradle-one-jar/'
def pluginRepo = 'https://github.com/TileDB/gradle-one-jar.git'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    into 'javadoc'
}

// enable Gradle Wrapper support for bootstrapping with specific version
task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

artifacts {
    archives jar
    archives sourcesJar
    archives groovydocJar
}

//********* artifact signing *********
if(gradle.startParameter.taskNames.contains("uploadArchives") || gradle.startParameter.taskNames.contains("publishPlugins")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

pluginBundle {
  website = pluginHome
  vcsUrl = pluginRepo
  description = pluginDesc
  tags = [pluginName, 'one-jar']

  plugins {
    greetingsPlugin {
      id = group + '.' + pluginName
      displayName = pluginName
    }
  }
}

uploadArchives {
    repositories {
        if (version.contains("SNAPSHOT")) {
            mavenLocal()
        } else {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                  authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                   name pluginName
                   packaging 'jar'
                   description pluginDesc
                   url pluginHome

                   scm {
                       url 'scm:git:' + pluginRepo
                       connection 'scm:git:' + pluginRepo
                       developerConnection 'scm:git:' + pluginRepo
                   }

                   licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                   }

                   developers {
                       developer {
                           id 'rholder'
                           name 'Ray Holder'
                       }
                       developer {
                            name 'Martin Blom'
                            email 'martin.blom@onslip.com'
                            organization 'Onslip AB'
                            organizationUrl 'http://www.onslip.com/'
                       }
                       developer {
                           name 'Seth Shelnutt'
                           email 'seth@tiledb.io'
                           organization 'TileDB, Inc.'
                           organizationUrl 'https://tiledb.io/'
                       }
                   }
               }
            }
        }
    }
}
